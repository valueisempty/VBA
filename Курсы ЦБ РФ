Option Explicit


'Процедура получения данных с сайта ЦБ РФ
Sub Exchange_Сurrency()
    Dim xmldoc As Object, nodeList As Object, node_attr As Object, xmlNode As Object
    Dim url_request_dollar$, url_request_euro$, sDateStart$, sDateFinish$
    Dim i&, iRow&
    Dim wsCurrency As Worksheet, wsSetup As Worksheet
    Dim arrCurrency() As Variant
        
    'Листы в рабочей книге
    Set wsCurrency = ThisWorkbook.Sheets("Курсы")
    Set wsSetup = ThisWorkbook.Sheets("Настройка")
    
    'Создаём OLE-объект DOMDocument
    Set xmldoc = CreateObject("Msxml.DOMDocument")
    'Отключаем асинхронную загрузку
    xmldoc.async = False
    
    'собираем URL с листа
    'url_request_dollar = "http://www.cbr.ru/scripts/XML_dynamic.asp?date_req1=01/01/2016&date_req2=14/03/2023&VAL_NM_RQ=R01235"
    With wsSetup
        sDateStart = Replace(.Cells(4, 2), ".", "/")
        sDateFinish = Replace(Date, ".", "/")
        url_request_dollar = .Cells(4, 1) & sDateStart & .Cells(4, 3) & sDateFinish & .Cells(4, 5) & .Cells(4, 6)     'Dollar
        url_request_euro = .Cells(4, 1) & sDateStart & .Cells(4, 3) & sDateFinish & .Cells(4, 5) & .Cells(5, 6)     'Euro
    End With
    
    'Масиив Дата, курс доллара, курс евро
    ReDim arrCurrency(0 To DateDiff("d", sDateStart, sDateFinish), 1 To 3)
    
    'Загружаем Url для доллара
    If xmldoc.Load(url_request_dollar) <> True Then
        Exit Sub
    End If
    'Получаем элементы узла Record
    Set nodeList = xmldoc.SelectNodes("*/Record")
    'Проходим по всему xml и записываем в массив
    For i = 0 To nodeList.Length - 1
        Set xmlNode = nodeList.Item(i)
        arrCurrency(i, 1) = xmlNode.getAttribute("Date")  'дата
        arrCurrency(i, 2) = xmlNode.SelectSingleNode("Value").Text  'курс доллара
    Next
    
    
    
    'Загружаем Url для евро
    If xmldoc.Load(url_request_euro) <> True Then
        Exit Sub
    End If
    'Получаем элементы узла Record
    Set nodeList = xmldoc.SelectNodes("*/Record")
    'Проходим по всему xml и записываем в массив
    For i = 0 To nodeList.Length - 1
        Set xmlNode = nodeList.Item(i)
        arrCurrency(i, 3) = xmlNode.SelectSingleNode("Value").Text  'курс евро
    Next
    
    iRow = 6
    'От самой новой даты
    For i = UBound(arrCurrency) To LBound(arrCurrency) Step -1
        If Not IsEmpty(arrCurrency(i, 1)) Then
            'Записываем массив на лист
            With wsCurrency
                .Cells(iRow, 1) = CDate(arrCurrency(i, 1))  'дата
                .Cells(iRow, 2) = CCur(arrCurrency(i, 2))  'курс доллара
                .Cells(iRow, 3) = CCur(arrCurrency(i, 3))  'курс евро
                iRow = iRow + 1
            End With
        End If
    Next
    
    'обновили сводную с средним занчение и записали дату обновления
    ThisWorkbook.RefreshAll
    wsCurrency.Cells(3, 5) = "Последнее обновление:"
    wsCurrency.Cells(4, 5) = Date
End Sub
